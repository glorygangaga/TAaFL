# Грамматика выражений языка CPY

## 1.Синтаксиса выражений

Выражения могут состоять из:

- **Литералы** чисел (целые и вещественные), строк и логических значений
- **Константы** `Pi` и `Euler`
- **Идентификаторы** переменных и функций
- **Вызовы функций** с аргументами
- **Унарные операторы**: `+`, `-`, `not`, `++` (префиксный), `--` (префиксный)
- **Бинарные операторы**: 
  - Арифметические: `+`, `-`, `*`, `/`, `//`, `%`, `**`
  - Сравнения: `==`, `!=`, `<`, `>`, `<=`, `>=`
  - Логические: `and`, `or`
- **Постфиксные операторы**: `++`, `--`
- **Оператор присваивания**: `=`
- **Тернарный оператор**: `? :`
- **Круглые скобки** для группировки выражений

---

## 2. Операторы

### 2.1. Арифметические операторы

| Символ | Операция                    | Тип              |
| ------ | --------------------------- | ---------------- |
| `+`    | Сложение или унарный плюс   | Бинарный/Унарный |
| `-`    | Вычитание или унарный минус | Бинарный/Унарный |
| `*`    | Умножение                   | Бинарный         |
| `/`    | Деление                     | Бинарный         |
| `//`   | Целочисленное деление       | Бинарный         |
| `%`    | Остаток от деления          | Бинарный         |
| `**`   | Возведение в степень        | Бинарный         |


### 2.2. Операторы инкремента и декремента

 | Символ | Операция        | Тип                    | Применение      |
 | ------ | --------------- | ---------------------- | --------------- |
 | `++`   | Увеличение на 1 | Префиксный/Постфиксный | `++x` или `x++` |
 | `--`   | Уменьшение на 1 | Префиксный/Постфиксный | `--x` или `x--` |


### 2.3. Операторы сравнения

| Символ | Описание         |
| :----- | :--------------- |
| `==`   | равно            |
| `!=`   | не равно         |
| `<`    | меньше           |
| `>`    | больше           |
| `<=`   | меньше или равно |
| `>=`   | больше или равно |


### 2.4. Логические операторы

| Символ | Операция       | Тип      |
| ------ | -------------- | -------- |
| `and`  | Логическое И   | Бинарный |
| `or`   | Логическое ИЛИ | Бинарный |
| `not`  | Логическое НЕ  | Унарный  |


### 2.5.Прочие операторы

| Символ | Операция                      | Тип         |
| ------ | ----------------------------- | ----------- |
| `=`    | Присваивание                  | Бинарный    |
| `? :`  | Тернарный оператор            | Тернарный   |
| `()`   | Вызов функции или группировка | Специальный |

---

## 3. Приоритет и ассоциативность операторов

| Приоритет (1 - наивысший) | Категория операторов            | Операторы                                | Ассоциативность |
| ------------------------- | ------------------------------- | ---------------------------------------- | --------------- |
| 1                         | Скобки, вызовы                  | `()`                                     | Слева           |
| 2                         | Постфиксные инкремент/декремент | `++`, `--` (постфиксные)                 | Слева           |
| 3                         | Префиксные операторы            | `++`, `--`, `+`, `-`, `not` (префиксные) | Справа          |
| 4                         | Возведение в степень            | `**`                                     | Справа          |
| 5                         | Мультипликативные операторы     | `*`, `/`, `%`, `//`                      | Слева           |
| 6                         | Аддитивные операторы            | `+`, `-` (бинарные)                      | Слева           |
| 7                         | Операторы сравнения             | `<`, `>`, `<=`, `>=`                     | Слева           |
| 8                         | Операторы равенства             | `==`, `!=`                               | Слева           |
| 9                         | Логическое И                    | `and`                                    | Слева           |
| 10                        | Логическое ИЛИ                  | `or`                                     | Слева           |
| 11                        | Тернарный оператор              | `? :`                                    | Справа          |
| 12                        | Присваивание                    | `=`                                      | Справа          |

---

## 4. Встроенные функции для чисел

| Имя              | Описание                        |
| ---------------- | ------------------------------- |
| `abs(x)`         | модуль числа                    |
| `min(a, b, ...)` | минимальное значение            |
| `max(a, b, ...)` | максимальное значение           |
| `pow(a, b)`      | возведение в степень            |
| `round(x)`       | округление до ближайшего целого |
| `ceil(x)`        | округление вверх                |
| `floor(x)`       | округление вниз                 |

 **Математические константы**

| Константа | Значение | Описание         |
| --------- | -------- | ---------------- |
| `Pi`      | 3.14159  | Число π (пи)     |
| `Euler`   | 2.71828  | Число e (Эйлера) |

---

## 5. Грамматика выражений (в нотации ISO EBNF)

```
(* === ВЫРАЖЕНИЯ === *)

expression =
ternary_expr ;

ternary_expr =
or_expr, [ "?", expression, ":", ternary_expr ] ;

or_expr =
and_expr, { "or", and_expr } ;

and_expr =
equality_expr, { "and", equality_expr } ;

equality_expr =
comparison_expr, { ("==" | "!="), comparison_expr } ;

comparison_expr =
arith_expr, { ("<" | ">" | "<=" | ">="), arith_expr } ;

arith_expr =
term_expr, { ("+" | "-"), term_expr } ;

term_expr =
prefix_expr, { ("*" | "/" | "%" | "//"), prefix_expr } ;

prefix_expr =
{ prefix_operator }, power_expr ;

prefix_operator =
"++" | "--" | "+" | "-" | "not" ;

power_expr =
postfix_expr, { "**", power_expr } ;

postfix_expr = primary_expr, { postfix_operator } ;

postfix_operator =
function_call
| "++"
| "--" ;

primary_expr =
identifier
| literal
| boolean
| constant
| input_expr 
| print_expr
| "(", expression, ")" ;

(* === ВЫРАЖЕНИЯ ВВОДА-ВЫВОДА === *)

input_expr = 
"input", "(", ")" ;

print_expr = 
"print", "(", [ expression_list ], ")" ;

(* === ПРИСВАИВАЕМЫЕ ВЫРАЖЕНИЯ === *)

assignable_expr =
identifier
| "(", assignable_expr, ")" ;

(* === ВЫЗОВЫ ФУНКЦИЙ И ЛИТЕРАЛЫ === *)

function_call = "(", [ argument_list ], ")" ;

expression_list = expression, { ",", expression } ;

argument_list = expression, { ",", expression } ;

```